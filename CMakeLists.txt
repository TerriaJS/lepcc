cmake_minimum_required(VERSION 3.16)

set(CMAKE_EXECUTABLE_SUFFIX ".html")

project(LEPCC CXX C)
string(TOLOWER ${PROJECT_NAME} PROJECT_NAME_LOWER)

set(ROOT_DIR "${PROJECT_SOURCE_DIR}")

file(GLOB BASE_SRCS src/*.cpp)
set(CMAKE_CXX_STANDARD 11)
message(INFO "files ${BASE_SRCS}")

set(LEPCC_INCLUDE_DIR "${PROJECT_SOURCE_DIR}/src/include")
message(INFO "LEPCC_INCLUDE_DIR ${LEPCC_INCLUDE_DIR}")

set(TEST_SRCS ${PROJECT_SOURCE_DIR}/src/Test_C_Api.cpp)
message(INFO "TEST_SRCS ${TEST_SRCS}")

list(REMOVE_ITEM BASE_SRCS ${TEST_SRCS})

message(INFO "files ${BASE_SRCS}")
add_library(lepcc "${BASE_SRCS}")

target_include_directories(
  lepcc
  PUBLIC "$<BUILD_INTERFACE:${PROJECT_SOURCE_DIR}>/src/include")

add_executable(lepcctest ${TEST_SRCS})
target_link_libraries(lepcctest lepcc)

# These are the `extern "c"` functions exported by the library that we
# wish to make available to JavaScript.
list(APPEND EXPORTED_FUNCTIONS
  lepcc_createContext
  lepcc_deleteContext
  lepcc_getBlobInfoSize
  lepcc_getBlobInfo
  lepcc_getPointCount
  lepcc_getRGBCount
  lepcc_getIntensityCount
  lepcc_getFlagByteCount
  lepcc_decodeXYZ
  lepcc_decodeRGB
  lepcc_decodeIntensity
  lepcc_decodeFlagBytes
  malloc
  free
)

foreach(FUNC ${EXPORTED_FUNCTIONS})
  list(APPEND EXPORTED_FUNCTIONS_QUOTED "'_${FUNC}'")
endforeach()

list(JOIN EXPORTED_FUNCTIONS_QUOTED "," EXPORTED_FUNCTIONS_PARAM)

add_executable(lepcc-wasm src/wasm.cpp)
target_link_libraries(lepcc-wasm lepcc)
target_link_options(
  lepcc-wasm
  PRIVATE
    "-sEXPORTED_FUNCTIONS=[${EXPORTED_FUNCTIONS_PARAM}]"
    "-sEXPORTED_RUNTIME_METHODS=['ccall','cwrap','setValue','getValue','writeArrayToMemory']"
    "-sMODULARIZE"
)

install(
  FILES
  "$<TARGET_FILE_DIR:lepcc-wasm>/lepcc-wasm.js"
  "$<TARGET_FILE_DIR:lepcc-wasm>/lepcc-wasm.wasm"
  TYPE BIN)